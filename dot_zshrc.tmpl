{{ if eq .chezmoi.os "darwin" -}}

# =============================================================================
# Environment & Path
# =============================================================================
export HOMEBREW_PREFIX="$(brew --prefix)"

# =============================================================================
# Plugins (Antidote)
# =============================================================================
# Initialize plugins statically with ${ZDOTDIR:-~}/.zsh_plugins.txt
# Must be loaded before compinit to ensure completions are registered
source "${HOMEBREW_PREFIX}/opt/antidote/share/antidote/antidote.zsh"
antidote load

# =============================================================================
# Options & History
# =============================================================================
setopt autocd
setopt correct
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_save_no_dups
setopt hist_find_no_dups

HISTSIZE=50000
SAVEHIST=50000
HISTDUP=erase

bindkey "^p" history-search-backward
bindkey "^n" history-search-forward
autoload -Uz select-word-style
select-word-style bash

# =============================================================================
# Completions
# =============================================================================
autoload -U compinit
compinit

# Set descriptions format to enable group support
zstyle ':completion:*:descriptions' format '[%d]'

# Set list-colors to enable filename colorizing
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

# Force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
zstyle ':completion:*' menu no

# Google Cloud SDK completion
if [[ -f "${HOMEBREW_PREFIX}/share/google-cloud-sdk/completion.zsh.inc" ]]; then
    source "${HOMEBREW_PREFIX}/share/google-cloud-sdk/completion.zsh.inc"
fi

# Terraform completion
complete -o nospace -C "$(command -v terraform)" terraform

# =============================================================================
# Tools & Integrations
# =============================================================================
# mise
eval "$(mise activate zsh)"

# oh-my-posh
if [ "$TERM_PROGRAM" != "Apple_Terminal" ] && [ "$TERM_PROGRAM" != "WarpTerminal" ]; then
    eval "$(oh-my-posh init zsh --config ~/.config/oh-my-posh/froggologies.omp.yaml)"
fi

# =============================================================================
# FZF Configuration
# =============================================================================
# Set up fzf key bindings and fuzzy completion
source <(fzf --zsh)

# Catppuccin Macchiato Theme
fzf_colors=(
  "bg+:#313244"           # Surface 0
  "spinner:#F5E0DC"       # Rosewater
  "hl:#F38BA8"            # Red
  "fg:#CDD6F4"            # Text
  "header:#F38BA8"        # Red
  "info:#CBA6F7"          # Mauve
  "pointer:#F5E0DC"       # Rosewater
  "marker:#B4BEFE"        # Lavender
  "fg+:#CDD6F4"           # Text
  "prompt:#CBA6F7"        # Mauve
  "hl+:#F38BA8"           # Red
  "selected-bg:#45475A"   # Surface 1
  "border:#6C7086"        # Overlay 0
  "label:#CDD6F4"         # Text
)
FZF_COLORS="${(j:,:)fzf_colors}"
unset fzf_colors

# Shared fzf flags
FZF_BASE_OPTS="
--style=full
--layout=reverse
--height=60%
--bind=ctrl-o:toggle-preview
--color=${FZF_COLORS}
"

export FZF_DEFAULT_OPTS="
${FZF_BASE_OPTS}
--preview 'fzf-preview {}'
--bind 'focus:transform-header:file --brief {}'
"

# fzf-tab configuration
zstyle ':fzf-tab:*' fzf-flags ${=FZF_BASE_OPTS} --preview-window=hidden

# fzf-tab default preview
zstyle ':fzf-tab:complete:*' fzf-preview 'fzf-preview ${realpath}'

# =============================================================================
# Aliases & Functions
# =============================================================================
alias ...="cd ../../"
alias ....="cd ../../../"
alias .....="cd ../../../../"
alias ls="eza --icons=auto --group-directories-first"
alias ll="ls -l"
alias la="ls -la"
alias lt="ls -T"
alias cb='for i in {0..15}; do printf "%3d: \e[48;5:${i}m   \e[0m " $i; [[ $((($i+1)%8)) == 0 ]] && echo; done'

man() {
  command man "$@" | col -bx | bat -l man --paging=always --style=plain
}

# Sketchybar interactivity overloads
function brew() {
  command brew "$@"

  if [[ $* =~ "upgrade" ]] || [[ $* =~ "update" ]] || [[ $* =~ "outdated" ]]; then
    sketchybar --trigger brew_update
  fi
}

# Set grc aliases
[[ -s "/opt/homebrew/etc/grc.zsh" ]] && source /opt/homebrew/etc/grc.zsh

{{- end }}